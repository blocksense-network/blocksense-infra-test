---
title: Historic Data Feed Store
---

import { Callout } from "@/components/common/Callout";
import { Tabs, TabsContent, TabsList, TabsTrigger } from '@blocksense/ui/Tabs';

# Historical Data Feed Store

Historical Data Feed Store contract is where all data is stored and where all contracts read from. It is written in Solidity assembly and contains only one function: `fallback`.

<Callout type="info" emoji="âš ï¸">
  This contract serves as the core logic that dictates how the
  [UpgradeableProxy](../../reference-documentation/UpgradeableProxy.mdx)
  contract manages its own storage, handling both data writing and reading
  operations. It's crucial to understand that all data is exclusively stored
  within the **UpgradeableProxy** contract. To access this data, users must
  interact with the `fallback` function of the **UpgradeableProxy** contract
  which underneath calls the `fallback` function of the
  **HistoricalDataFeedStore** contract.
</Callout>

## Code Examples

### Solidity

<Callout type="info" emoji="ðŸ’¡">
  Use [Blocksense library](/coming-soon) for easy and gas optimised interaction
  with the contract.
</Callout>

Here is a complete working example. After that, there are more details on how to perform a raw call.

```solidity showLineNumbers copy filename="UpgradeableProxyConsumer.sol"
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import {Blocksense} from 'lib/Blocksense.sol';

/**
 * THIS IS AN EXAMPLE CONTRACT THAT USES UN-AUDITED CODE.
 * DO NOT USE THIS CODE IN PRODUCTION.
 */
contract UpgradeableProxyConsumer {
  address public immutable dataFeedStore;

  constructor(address feedAddress) {
    dataFeedStore = feedAddress;
  }

  function getDataById(
    uint32 key
  ) external view returns (uint256 value, uint64 timestamp) {
    bytes32 data = Blocksense._callDataFeed(
      dataFeedStore,
      abi.encodePacked(0x80000000 | key)
    );

    return (uint256(uint192(bytes24(data))), uint64(uint256(data)));
  }

  function getFeedAtCounter(
    uint32 key,
    uint32 counter
  ) external view returns (uint256 value, uint64 timestamp) {
    bytes32 data = Blocksense._callDataFeed(
      dataFeedStore,
      abi.encodeWithSelector(bytes4(0x20000000 | key), counter)
    );

    return (uint256(uint192(bytes24(data))), uint64(uint256(data)));
  }

  function getLatestCounter(uint32 key) external view returns (uint32 counter) {
    return uint32(Blocksense._latestRound(key, dataFeedStore));
  }

  function getLatestRoundData(
    uint32 key
  ) external view returns (int256 value, uint256 timestamp, uint80 counter) {
    (counter, value, timestamp, , ) = Blocksense._latestRoundData(
      key,
      dataFeedStore
    );
  }
}
```

In order to retrieve data from the contract, the client must call the `fallback` function as follows:

- To read the latest price and timestamp:

<Tabs defaultValue="proxy-call" className="mt-4">
  <TabsList>
    <TabsTrigger value="proxy-call">Proxy Call</TabsTrigger>
    <TabsTrigger value="raw-call">Raw Call</TabsTrigger>
  </TabsList>
    <TabsContent value="proxy-call">
      ```solidity copy filename="solidity"
      function getDataById(
        uint32 key
      ) external view returns (uint256 value, uint64 timestamp) {
        bytes32 data = Blocksense._callDataFeed(
          dataFeedStore,
          abi.encodePacked(0x80000000 | key)
        );

        return (uint256(uint192(bytes24(data))), uint64(uint256(data)));
      }
      ```
    </TabsContent>

      <TabsContent value="raw-call">
      ```solidity copy filename="solidity"
      function getDataById(
        uint32 key
      ) external view returns (uint256 value, uint64 timestamp) {
        (bool success, bytes memory returnData) = dataFeedStore.staticcall(
          abi.encodePacked(0x80000000 | key)
        );
        require(success, "DataFeedStore: call failed");

        bytes32 data = bytes32(returnData);
        return (uint256(uint192(bytes24(data))), uint64(uint256(data)));
      }
      ```
    </TabsContent>

</Tabs>

- To read historical data for a specific round:

<Tabs defaultValue="proxy-call" className="mt-4">
  <TabsList>
    <TabsTrigger value="proxy-call">Proxy Call</TabsTrigger>
    <TabsTrigger value="raw-call">Raw Call</TabsTrigger>
  </TabsList>
    <TabsContent value="proxy-call">
      ```solidity copy filename="solidity"
      function getFeedAtCounter(
        uint32 key,
        uint32 counter
      ) external view returns (uint256 value, uint64 timestamp) {
        bytes32 data = Blocksense._callDataFeed(
          dataFeedStore,
          abi.encodeWithSelector(bytes4(0x20000000 | key), counter)
        );

        return (uint256(uint192(bytes24(data))), uint64(uint256(data)));
      }
      ```
    </TabsContent>

    <TabsContent value="raw-call" className="mt-4">
      ```solidity copy filename="solidity"
      function getFeedAtCounter(
        uint32 key,
        uint32 counter
      ) external view returns (uint256 value, uint64 timestamp) {
        (bool success, bytes memory returnData) = dataFeedStore.staticcall(
          abi.encodeWithSelector(bytes4(0x20000000 | key), counter)
        );
        require(success, "DataFeedStore: call failed");

        bytes32 data = bytes32(returnData);
        return (uint256(uint192(bytes24(data))), uint64(uint256(data)));
      }
      ```
    </TabsContent>

</Tabs>

- To get the latest round:

<Tabs defaultValue="proxy-call" className="mt-4">
  <TabsList>
    <TabsTrigger value="proxy-call">Proxy Call</TabsTrigger>
    <TabsTrigger value="raw-call">Raw Call</TabsTrigger>
  </TabsList>
    <TabsContent value="proxy-call">
      ```solidity copy filename="solidity"
      function getLatestCounter(uint32 key) external view returns (uint32 counter) {
        return uint32(Blocksense._latestRound(key, dataFeedStore));
      }
      ```
    </TabsContent>

    <TabsContent value="raw-call">
      ```solidity copy filename="solidity"
      function getLatestCounter(uint32 key) external view returns (uint32 counter) {
        (bool success, bytes memory returnData) = dataFeedStore.staticcall(
          abi.encodePacked(0x40000000 | key)
        );
        require(success, "DataFeedStore: call failed");

        (, counter) = abi.decode(returnData, (bytes32, uint32));
      }
      ```
    </TabsContent>

</Tabs>

- To get the latest round data:

<Tabs defaultValue="proxy-call" className="mt-4">
  <TabsList>
    <TabsTrigger value="proxy-call">Proxy Call</TabsTrigger>
    <TabsTrigger value="raw-call">Raw Call</TabsTrigger>
  </TabsList>
    <TabsContent value="proxy-call">
      ```solidity copy filename="solidity"
      function getDataById(
        uint32 key
      ) external view returns (int256 value, uint256 timestamp, uint80 counter) {
        (counter, value, timestamp, , ) = Blocksense
          ._latestRoundData(key, dataFeedStore);
      }
      ```
    </TabsContent>

    <TabsContent value="raw-call">
      ```solidity copy filename="solidity"
      function getDataById(
        uint32 key
      ) external view returns (uint256 value, uint64 timestamp, uint256 counter) {
        (bool success, bytes memory returnData) = dataFeedStore.staticcall(
          abi.encodePacked(0xc0000000 | key)
        );
        require(success, "DataFeedStore: call failed");

        (bytes32 data1, bytes32 data2) = abi.decode(returnData, (bytes32, bytes32));

        value = uint256(uint192(bytes24(data1)));
        timestamp = uint64(uint256(data1));
        counter = uint256(data2);
      }
      ```
    </TabsContent>

</Tabs>

### Solidity Hardhat Example

<Callout type="info" emoji="ðŸ’¡">
  You can find a working Hardhat project [here](/coming-soon). Clone the repo
  and follow the setup instructions to run the example locally.
</Callout>

### Ethers.js v6.x

To read the latest value and timestamp:

```js copy filename="javascript"
const historicDataFeedStore = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);
const data = '0x' + ((key | 0x80000000) >>> 0).toString(16).padStart(8, '0');

const res = await network.provider.send('eth_call', [
  {
    to: historicDataFeedStore.target,
    data,
  },
  'latest',
]);

const value = Number(res.slice(0, 50));
const timestamp = Number('0x' + res.slice(50, 66));
```

To read historical data for a specific round:

```js copy filename="javascript"
const historicDataFeedStore = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);
const round = 1;
const data = '0x' + ((key | 0x20000000) >>> 0).toString(16).padStart(8, '0');

const res = await network.provider.send('eth_call', [
  {
    to: historicDataFeedStore.target,
    data: ethers.solidityPacked(['bytes4', 'uint256'], [data, round]),
  },
  'latest',
]);

const value = Number(res.slice(0, 50));
const timestamp = Number('0x' + res.slice(50, 66));
```

To get the latest round:

```js copy filename="javascript"
const historicDataFeedStore = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);
const data = '0x' + ((key | 0x40000000) >>> 0).toString(16).padStart(8, '0');

const res = await network.provider.send('eth_call', [
  {
    to: historicDataFeedStore.target,
    data,
  },
  'latest',
]);
const round = Number('0x' + res.slice(66));
```

To get the latest round data:

```js copy filename="javascript"
const historicDataFeedStore = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);
const data = '0x' + ((key | 0xc0000000) >>> 0).toString(16).padStart(8, '0');

const res = await network.provider.send('eth_call', [
  {
    to: historicDataFeedStore.target,
    data,
  },
  'latest',
]);

const value = Number(res.slice(0, 50));
const timestamp = Number('0x' + res.slice(50, 66));
const round = Number('0x' + res.slice(66));
```
